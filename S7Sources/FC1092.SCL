FUNCTION FC1092 : VOID
NAME    : 'FC_DIO'
AUTHOR  : 'YACINEAA'
FAMILY  : 'DIO'
VERSION : '1.5'


//GNU General Public License v3.0
//--------------------------------------------------------------------------------------------------------------------------------------
//-- Object         Management of Digital input/output (DIO)
//-- Type           Function
//-- Version        1
//--------------------------------------------------------------------------------------------------------------------------------------
//--Author          Yacine A.A.
//--Organization    2020 COPAG, Industrial project management office, Department of operational technology / Taroudant, Morocco          
//--Description     This block takes care of handling for Digital inputs or outputs, this is in particular:
//                      - The activation of the object in the different modes
//                      - Turn-on delay and Turn-off delay of the object in the different modes
//                      - Interlocking the manual mode of the object
//                      - Faults calculations     
//                      - Operations counters   
//                      - Operation time
//-----------------------------------------------------------------------------------------------------------------
//--History
//--[Version]            [Name]                        [DATE]                    [Comments]
//--1.00                 Yacine AA                     13-01-21                  Creation
//--1.50              Yacine AA                     07-02-21                  Replacing STRUCT with ARRAY to gain memrory space  
//--------------------------------------------------------------------------------------------------------------------------------------
//-----ToDo
//-- Calculate Hardware fault via module diagnostic 
//-- 
//---
//---
//--------------------------------------------------------------------------------------------------------------------------------------
//-----HELP
//----TypeID : 
//--- 0 : Digital input with CIP mode
//--- 1 : Digital input
//--- 3 : Digital output
//--- 2 : input or output with cycle counter 
//----ARRAYS : 
// Setting_00 : INT  : ID of the digital object  
// Setting_01 : INT  : Manual forcing period (0 if permanent) [100ms]
// Setting_02 : INT  : Grace period before activation output [100ms]
// Setting_03 : INT  : Grace period before deactivation output [100ms]
// Setting_04 : INT  : Grace period before activation output in CIP mode [100ms]
// Setting_05 : INT  : Grace period before deactivation output in CIP mode [100ms]
// Setting_06 : INT  : Spare [100ms]
// Buffer_00 : INT  : Manual forcing period [100ms]
// Buffer_01 : INT  : Grace period before activation output [100ms]
// Buffer_02 : INT  : Grace period before deactivation output [100ms]
// Buffer_03 : INT  : Spare [100ms]
// Register_00 : REAL : Object operating time [h]
// Register_01 : INT : Object cycle counter
// Register_02 : INT : faults counter
// Register_03 : INT : Manual mode counter
// Register_04 : INT : Spare
// AccessLevel_00 : INT : Access level 0 : for manual operation
// AccessLevel_01 : INT : Access level 1 ; for inhibition mode 
// AccessLevel_02 : INT : Access level 2 : for setting level 
// AccessLevel_03 : INT : Access level 3 : for advanced setting level




VAR_IN_OUT
    DIO                      : UDT_DIO ; //Standerd user data type for Digital inputs or outputs DIO
END_VAR


VAR_TEMP
    PostiveEdge100ms        : BOOL ; //Positive edge for 100ms (10Hz) 
    PostiveEdge1s           : BOOL ; //Positive edge for 1s (1Hz) 
    Acknowledgment          : BOOL ; //Acknowledgment for motor zone
    Simulation              : BOOL ; //Simulation mode in motor zone
    PowerOn                 : BOOL ; //Power on signal 
    Ack                     : BOOL ; //Acknowledgment temporary working bit 
    Setting00               : INT  ; //Grace period before activation output [100ms]
    Setting01               : INT  ; //Grace period before activation output [100ms]
END_VAR 



BEGIN

//Loading utilities
PostiveEdge100ms   := "Frm_100ms" ;
PostiveEdge1s      := "Frm_1s" ; 
Acknowledgment     := "DB_COM_HMI".ACQ_DE_HMI_MEM ;
Simulation         := "MODE_SIM" ; 
PowerOn            := True;

//Simulation = Zone Simulation or element Simulation
DIO.WorkinMode0 := Simulation OR "DB_SCADA".SimulationMode_0 ;

//Acknowledgment = Zone acknowledgment or element acknowledgment
Ack := DIO.Acknowledgment OR Acknowledgment;
//Resetting faults
IF Ack THEN
    DIO.Fault_00     := False;
    DIO.Fault_01     := False;
    DIO.Fault_02     := False;
    DIO.Fault_03     := False;
    DIO.Fault_04     := False;
    DIO.Fault_05     := False;
    DIO.Fault_06     := False;
    DIO.Fault_07     := False;
    DIO.Acknowledged := True;
END_IF;

//Deny manual modes
IF DIO.WorkinMode4  THEN DIO.WorkinMode3 := False; DIO.ForceValue := False; END_IF;

//CIP mode
IF DIO.Setting[0] = 0 THEN 
    IF DIO.WorkinMode2  THEN 
        Setting00 := DIO.Setting[4] ;
        Setting01 := DIO.Setting[5] ;
    ELSE
        Setting00 := DIO.Setting[2] ;
        Setting01 := DIO.Setting[3] ;
    END_IF;
ELSE
    DIO.WorkinMode2 := False;
END_IF;
        

//Simulation & suppression modes
IF DIO.WorkinMode0 OR DIO.WorkinMode1 THEN 
    IF NOT DIO.Edge[8] THEN DIO.Acknowledgment := True ; END_IF;
END_IF;

//Manual mode
IF DIO.WorkinMode3 THEN
    DIO.Bit[0] := DIO.ForceValue ;
    Setting00   := 0 ;
    Setting01   := 0 ;
    //Positive edge
    IF NOT DIO.Edge[9] THEN 
        DIO.Buffer[0] := 0; 
        DIO.Register[3]       := DIO.Register[3] + 1 ;
    END_IF;
    IF PostiveEdge100ms AND DIO.Buffer[0] < DIO.Setting[1] THEN DIO.Buffer[0] := DIO.Buffer[0] + 1; END_IF;
    IF DIO.Buffer[0] >= DIO.Setting[1] AND DIO.Setting[1] > 0  THEN DIO.WorkinMode3 := False; DIO.ForceValue := False; END_IF;
ELSE
    //Automatic modes
    DIO.Bit[0] := DIO.Input ;
END_IF;

//Writting to value
IF DIO.Bit[0] THEN
    //Positive edge
    IF NOT DIO.Edge[0] THEN 
        DIO.Buffer[1] := 0;
        DIO.Register[1]       := DIO.Register[1] + 1;
    END_IF;
    IF PostiveEdge100ms AND DIO.Buffer[1] < Setting00 THEN DIO.Buffer[1] := DIO.Buffer[1] + 1; END_IF;
    IF DIO.Buffer[1] >= Setting00 OR Setting00 <= 0   THEN DIO.Value := DIO.Bit[0]; END_IF;
ELSE
    //Negative edge
    IF  DIO.Edge[0] THEN 
        DIO.Buffer[2] := 0 ; 
    END_IF;
    IF PostiveEdge100ms AND DIO.Setting[2] < Setting01 THEN DIO.Buffer[2] := DIO.Buffer[2] + 1; END_IF;
    IF DIO.Buffer[2] >= Setting01 OR Setting01 <= 0   THEN DIO.Value := DIO.Bit[0]; END_IF;
END_IF; 

//Object in unconformity state (externally set) : 01
IF False THEN DIO.Fault_01 := False ; END_IF;

//Hardware fault : 02
IF False THEN DIO.Fault_02 := False ; END_IF;

//Loss of power fault : 03
IF NOT PowerOn THEN DIO.Fault_03 := True ; END_IF;

//Maximum operating time before preventive maintenance reached : 04
IF (DIO.Register[1] > 70000) AND DIO.Setting[0] = 2 THEN DIO.Fault_04 := True ; END_IF;

//Summing main faults : 00
IF (DIO.Fault_01 OR DIO.Fault_02 OR DIO.Fault_03) AND NOT (DIO.WorkinMode0 OR DIO.WorkinMode1) THEN
    IF NOT DIO.Fault_00 THEN DIO.Register[2] := DIO.Register[2] + 1; END_IF;
    DIO.Fault_00 := True ;
ELSE
    DIO.Acknowledged := False;
END_IF;

//Access levels monitoring
IF DIO.AccessLevel[3] > 9999                THEN DIO.AccessLevel[3] := 9999; END_IF;
IF DIO.AccessLevel[3] < DIO.AccessLevel[2] THEN DIO.AccessLevel[3] := DIO.AccessLevel[2] + 1; END_IF;
IF DIO.AccessLevel[2] < DIO.AccessLevel[1] THEN DIO.AccessLevel[2] := DIO.AccessLevel[1] + 1; END_IF;
IF DIO.AccessLevel[1] < DIO.AccessLevel[0] THEN DIO.AccessLevel[1] := DIO.AccessLevel[0] + 1; END_IF;
IF DIO.AccessLevel[0] < 0                   THEN DIO.AccessLevel[0] := 0; END_IF;

//Edgging
DIO.Edge[0] := DIO.Bit[0] ; 

DIO.Edge[8] := DIO.WorkinMode0 OR DIO.WorkinMode1 ; 

DIO.Edge[9] := DIO.WorkinMode3 ;


//Main operating time [h]
IF PostiveEdge1s AND DIO.Value THEN 
    DIO.Register_00 := DIO.Register_00 + (1.0/3600.0); 
END_IF;

//Calulating number of forced, inhibited and in fault elements [Legacy]
IF DIO.WorkinMode3   THEN "DB_SCADA".DIOForc  := "DB_SCADA".DIOForc  + 1 ; END_IF;
IF DIO.WorkinMode1   THEN "DB_SCADA".DIOInhib := "DB_SCADA".DIOInhib + 1 ; END_IF;
IF DIO.Fault_00      THEN "DB_SCADA".DIOFault := "DB_SCADA".DIOFault + 1 ; END_IF;

//Resetting Acknowledgment
IF Ack AND NOT (DIO.WorkinMode0 OR DIO.WorkinMode1) THEN DIO.Acknowledgment:=FALSE; END_IF;

END_FUNCTION
 

